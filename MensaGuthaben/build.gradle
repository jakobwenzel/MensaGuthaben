buildscript {

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.5.0'
    }
}
apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    google()
}

dependencies {
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'org.apache.commons:commons-lang3:3.14.0'

}

android {

    defaultConfig {
        minSdkVersion 14
        compileSdk 34
        targetSdkVersion 34
    }
    
    signingConfigs {
        release {
            // We can leave these in environment variables
            storeFile file(System.getenv("KEYSTORE") ?: "keystore")
            keyAlias System.getenv("KEY_ALIAS") ?: "jakobwenzel"

            // These two lines make gradle believe that the signingConfigs
            // section is complete. Without them, tasks like installRelease
            // will not be available!
            storePassword "notYourRealPassword"
            keyPassword "notYourRealPassword"
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    namespace 'de.yazo_games.mensaguthaben'
}

tasks.register('askForPassword') {
    doLast {
        def storePw = PasswordPrompt.readPassword("Keystore password") ?: "invalid"
        //If it's null, gradle complains


        android.signingConfigs.release.storePassword = storePw
        android.signingConfigs.release.keyPassword = storePw
    }
}

tasks.configureEach { theTask ->
    if (theTask.name.equals("packageRelease")) {
        theTask.dependsOn "askForPassword"
    }
}

